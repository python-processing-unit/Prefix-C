# Stage 4 Test - Tests INT, FLT, STR, FUNC and basic operators
# Excludes TNS, MAP, THR operations

PRINT("=== Stage 4 Tests ===")

# --- Literal formats ---
PRINT("Testing literals...")
INT: i0 = 0
INT: i1 = 1
INT: i5 = 101   # 5
INT: i_neg = -101   # -5 (signed literal)
ASSERT(EQ(i0, 0))
ASSERT(EQ(i1, 1))
ASSERT(EQ(i5, 101))
ASSERT(EQ(i_neg, -101))
DEL(i0)
DEL(i1)
DEL(i5)
DEL(i_neg)
PRINT("Literals: PASS")

# Floating-point binary fixed-point forms
PRINT("Testing floats...")
FLT: f_half = 0.1    # 0.5
FLT: f_quarter = 0.01 # 0.25
FLT: f_threeq = 0.11  # 0.75
ASSERT(EQ(ADD(f_half, f_quarter), f_threeq))
DEL(f_half)
DEL(f_quarter)
DEL(f_threeq)
PRINT("Floats: PASS")

# String literals
PRINT("Testing strings...")
STR: s1 = "Hello, World!"
ASSERT(EQ(SLEN(s1), 1101))  # 13 in binary
STR: s2 = "test"
ASSERT(EQ(UPPER(s2), "TEST"))
ASSERT(EQ(LOWER("TEST"), "test"))
DEL(s1)
DEL(s2)
PRINT("Strings: PASS")

# Tensor literals
PRINT("Testing tensors...")
TNS: t = [[1, 10], [11, 100]]
ASSERT(t[1, 10])
ASSERT(EQ(t[1, 1-10], [1, 10]))
ASSERT(EQ(t[*, *], t))
ASSERT(EQ(t[*, 1], [1, 11]))
ASSERT(EQ(t[1-10, 1-10], t[*, *]))
ASSERT(EQ(t[1--1, 1], [1, 11]))
DEL(t)
PRINT("Tensors: PASS")

PRINT("=== MAP tests ===")
# Simple map literal and lookup
MAP: m = <"foo" = 101, "bar" = 1010, "nested" = <"a" = 1, "b" = 10>>
ASSERT(EQ(m<"foo">, 101))
ASSERT(EQ(m<"bar">, 1010))
ASSERT(EQ(m<"nested", "a">, 1))
ASSERT(EQ(m<"nested", "b">, 10))
# Missing key should return null/0 (treat as false)
ASSERT(NOT(m<"nope">))
PRINT("=== MAP tests PASSED ===")


# --- TNS operator ---
PRINT("Testing TNS operator...")
# string -> 1-D tensor of single-character STRs
ASSERT(EQ(TNS("foo"), ["f","o","o"]))
# shape + value -> filled tensor
TNS: shp = [10]   # shape [10] -> length 2
ASSERT(EQ(TNS(shp, 101), [101, 101]))
DEL(shp)
PRINT("TNS operator: PASS")

# --- TINT / TFLT / TSTR ---
PRINT("Testing TINT/TFLT/TSTR...")
TNS: strnums = ["101", "0", ""]
TNS: as_ints = TINT(strnums)
ASSERT(EQ(as_ints[1], 101))
ASSERT(EQ(as_ints[10], 0))
ASSERT(EQ(as_ints[11], 0))
TNS: as_flts = TFLT(["0.1", "1", "10"])  # 0.5,1.0,2.0
ASSERT(EQ(as_flts[1], 0.1))
ASSERT(EQ(as_flts[10], 1.0))
ASSERT(EQ(as_flts[11], 10.0))
TNS: as_strs = TSTR([1, 10, 11])
ASSERT(EQ(as_strs[1], "1"))
ASSERT(EQ(as_strs[10], "10"))
ASSERT(EQ(as_strs[11], "11"))
DEL(strnums)
DEL(as_ints)
DEL(as_flts)
DEL(as_strs)
PRINT("TINT/TFLT/TSTR: PASS")

# --- CONV ---
PRINT("Testing CONV...")
# Simple 1-D identity kernel
TNS: c_in = [1, 10, 11]
TNS: c_k = [1]
ASSERT(EQ(CONV(c_in, c_k), c_in))
DEL(c_in)
DEL(c_k)

# Simple 2-D identity kernel
TNS: img = [[1, 10], [11, 100]]
TNS: k2 = [[1]]
ASSERT(EQ(CONV(img, k2), img))
DEL(img)
DEL(k2)
PRINT("CONV: PASS")

# --- FILL operator ---
PRINT("Testing FILL...")
TNS: base_fill = [1, 10, 11]
TNS: filled = FILL(base_fill, 0)
ASSERT(EQ(filled[1], 0))
ASSERT(EQ(filled[10], 0))
ASSERT(EQ(filled[11], 0))
DEL(base_fill)
DEL(filled)
PRINT("FILL: PASS")

# --- SHAPE / TLEN / TFLIP ---
PRINT("Testing SHAPE/TLEN/TFLIP...")
TNS: t2 = [[1, 10], [11, 100]]
ASSERT(EQ(SHAPE(t2), [10, 10]))
ASSERT(EQ(TLEN(t2, 1), 10))
ASSERT(EQ(TLEN(t2, 10), 10))
ASSERT(EQ(TFLIP(t2, 1), [[11, 100], [1, 10]]))
ASSERT(EQ(TFLIP(t2, 10), [[10, 1], [100, 11]]))
DEL(t2)
PRINT("SHAPE/TLEN/TFLIP: PASS")

# --- SCAT ---
PRINT("Testing SCAT...")
TNS: dst_scat = [[0,0,0],[0,0,0]]
TNS: src_scat = [[1, 10],[11, 100]]
TNS: ind_scat = [[1, 10],[1, 10]]
ASSERT(EQ(SCAT(src_scat, dst_scat, ind_scat), [[1, 10, 0], [11, 100, 0]]))
DEL(dst_scat)
DEL(src_scat)
DEL(ind_scat)
PRINT("SCAT: PASS")

# --- Tensor-scalar arithmetic operators ---
PRINT("Testing tensor-scalar arithmetic...")
TNS: ta = [1, 10]
ASSERT(EQ(TADD(ta, 1), [10, 11]))    # [1,2] + 1 -> [2,3]
ASSERT(EQ(TSUB(ta, 1), [0, 1]))     # [1,2] - 1 -> [0,1]
ASSERT(EQ(TMUL(ta, 10), [10, 100])) # [1,2] * 2 -> [2,4]
ASSERT(EQ(TDIV(ta, 10), [0, 1]))    # [1,2] / 2 -> [0,1]
ASSERT(EQ(TPOW(ta, 10), [1, 100]))  # [1,2]^2 -> [1,4]
# scalar on left for non-commutative
ASSERT(EQ(TSUB(10, ta), [1, 0]))
DEL(ta)
PRINT("Tensor-scalar arithmetic: PASS")

# --- Matrix (elementwise) arithmetic operators ---
PRINT("Testing matrix (elementwise) arithmetic...")
TNS: m1 = [[1, 10], [11, 100]]
TNS: m2 = [[10, 1], [100, 11]]
ASSERT(EQ(MADD(m1, m2), [[11, 11], [111, 111]]))
ASSERT(EQ(MSUB(m1, m2), [[-1, 1], [-1, 1]]))
ASSERT(EQ(MMUL(m1, m2), [[10, 10], [1100, 1100]]))
ASSERT(EQ(MDIV(m1, m2), [[0, 10], [0, 1]]))
DEL(m1)
DEL(m2)
PRINT("Matrix arithmetic: PASS")

# --- MSUM / MPROD ---
PRINT("Testing MSUM/MPROD...")
TNS: ma1 = [[1, 10], [11, 100]]
TNS: ma2 = [[10, 1], [100, 11]]
TNS: ma3 = [[1, 1], [1, 1]]
ASSERT(EQ(MSUM(ma1, ma2, ma3), [[100, 100], [1000, 1000]]))
ASSERT(EQ(MSUM(ma1, ma2), [[11, 11], [111, 111]]))
ASSERT(EQ(MPROD(ma1, ma2, ma3), [[10, 10], [1100, 1100]]))
DEL(ma1)
DEL(ma2)
DEL(ma3)
PRINT("MSUM/MPROD: PASS")

# --- Arithmetic operators ---
PRINT("Testing arithmetic...")
ASSERT(EQ(ADD(10, 11), 101))     # 2 + 3 = 5
ASSERT(EQ(SUB(101, 10), 11))     # 5 - 2 = 3
ASSERT(EQ(MUL(10, 11), 110))     # 2 * 3 = 6
ASSERT(EQ(DIV(110, 10), 11))     # 6 / 2 = 3
ASSERT(EQ(MOD(111, 10), 1))      # 7 % 2 = 1
ASSERT(EQ(POW(10, 11), 1000))    # 2^3 = 8
ASSERT(EQ(NEG(101), -101))       # -5
ASSERT(EQ(ABS(-101), 101))       # 5
PRINT("Arithmetic: PASS")

# --- Comparison operators ---
PRINT("Testing comparisons...")
ASSERT(EQ(10, 10))
ASSERT(NOT(EQ(10, 11)))
ASSERT(GT(11, 10))
ASSERT(LT(10, 11))
ASSERT(GTE(11, 10))
ASSERT(GTE(10, 10))
ASSERT(LTE(10, 11))
ASSERT(LTE(10, 10))
PRINT("Comparisons: PASS")

# --- Logical operators ---
PRINT("Testing logical ops...")
ASSERT(AND(1, 1))
ASSERT(NOT(AND(1, 0)))
ASSERT(OR(1, 0))
ASSERT(OR(0, 1))
ASSERT(NOT(OR(0, 0)))
ASSERT(XOR(1, 0))
ASSERT(XOR(0, 1))
ASSERT(NOT(XOR(1, 1)))
ASSERT(NOT(0))
ASSERT(NOT(NOT(1)))
PRINT("Logical: PASS")

# --- Bitwise operators ---
PRINT("Testing bitwise ops...")
ASSERT(EQ(BAND(111, 101), 101))   # 7 & 5 = 5
ASSERT(EQ(BOR(100, 10), 110))     # 4 | 2 = 6
ASSERT(EQ(BXOR(111, 101), 10))    # 7 ^ 5 = 2
ASSERT(EQ(SHL(1, 10), 100))       # 1 << 2 = 4
ASSERT(EQ(SHR(1000, 10), 10))     # 8 >> 2 = 2
PRINT("Bitwise: PASS")

# --- Type conversions ---
PRINT("Testing type conversions...")
ASSERT(EQ(INT(0.1), 0))           # FLT->INT truncates
ASSERT(EQ(FLT(10), 10.0))         # INT->FLT
ASSERT(EQ(STR(101), "101"))       # INT->STR
ASSERT(EQ(INT("101"), 101))       # STR->INT
PRINT("Type conversions: PASS")

# --- Type checking ---
PRINT("Testing type checks...")
INT: x = 101
FLT: y = 1.1
STR: z = "test"
ASSERT(ISINT(x))
ASSERT(NOT(ISINT(y)))
ASSERT(ISFLT(y))
ASSERT(NOT(ISFLT(x)))
ASSERT(ISSTR(z))
ASSERT(NOT(ISSTR(x)))
ASSERT(EQ(TYPE(x), "INT"))
ASSERT(EQ(TYPE(y), "FLT"))
ASSERT(EQ(TYPE(z), "STR"))
DEL(x)
DEL(y)
DEL(z)
PRINT("Type checks: PASS")

# --- String operations ---
PRINT("Testing string ops...")
STR: s = "Hello, World!"
ASSERT(EQ(SLICE(s, 1, 101), "Hello"))  # positions 1-5
ASSERT(EQ(REPLACE(s, "World", "Prefix"), "Hello, Prefix!"))
ASSERT(EQ(STRIP("  test  ", " "), "test"))
# FLIP tests (INT and STR)
ASSERT(EQ(FLIP(110), 11))    # 6 -> 3
ASSERT(EQ(FLIP(-110), -11))  # -6 -> -3 (sign preserved)
ASSERT(EQ(FLIP(0), 0))
ASSERT(EQ(FLIP("abc"), "cba"))
PRINT("String ops: PASS")
DEL(s)

# --- Variadic math ---
PRINT("Testing variadic math...")
ASSERT(EQ(SUM(1, 10, 11), 110))    # 1+2+3 = 6
ASSERT(EQ(PROD(10, 11), 110))      # 2*3 = 6
ASSERT(EQ(MAX(1, 101, 11), 101))   # max(1,5,3) = 5
ASSERT(EQ(MIN(1, 101, 11), 1))     # min(1,5,3) = 1
ASSERT(ANY(0, 0, 1))
ASSERT(NOT(ANY(0, 0, 0)))
ASSERT(ALL(1, 1, 1))
ASSERT(NOT(ALL(1, 0, 1)))
PRINT("Variadic math: PASS")

# --- MAX/MIN with tensors ---
PRINT("Testing MAX/MIN with tensors...")
TNS: tm1 = [1, 10, 11]
TNS: tm2 = [[1, 100], [11, 10]]
ASSERT(EQ(MAX(tm1), 11))
ASSERT(EQ(MIN(tm1), 1))
ASSERT(EQ(MAX(tm2), 100))
ASSERT(EQ(MIN(tm2), 1))

TNS: ts = ["a", "abcd", "ab"]
ASSERT(EQ(MAX(ts), "abcd"))
ASSERT(EQ(MIN(ts), "a"))
DEL(tm1)
DEL(tm2)
DEL(ts)
PRINT("MAX/MIN with tensors: PASS")

# --- Control flow: IF ---
PRINT("Testing IF...")
INT: result = 0
IF(1){
    result = 1
}
ASSERT(EQ(result, 1))

result = 0
IF(0){
    result = 1
}ELSEIF(1){
    result = 10
}
ASSERT(EQ(result, 10))

result = 0
IF(0){
    result = 1
}ELSEIF(0){
    result = 10
}ELSE{
    result = 11
}
ASSERT(EQ(result, 11))
DEL(result)
PRINT("IF: PASS")

# --- Control flow: WHILE ---
PRINT("Testing WHILE...")
INT: counter = 0
INT: sum = 0
WHILE(LT(counter, 101)){
    sum = ADD(sum, counter)
    counter = ADD(counter, 1)
}
ASSERT(EQ(sum, 1010))  # 0+1+2+3+4 = 10
DEL(counter)
DEL(sum)
PRINT("WHILE: PASS")

# --- Control flow: FOR ---
PRINT("Testing FOR...")
INT: total = 0
FOR(i, 101){  # 5 iterations: 0,1,2,3,4
    total = ADD(total, i)
}
ASSERT(EQ(total, 1010))  # 0+1+2+3+4 = 10
DEL(total)
PRINT("FOR: PASS")

# --- Control flow: BREAK ---
PRINT("Testing BREAK...")
INT: count = 0
WHILE(1){
    count = ADD(count, 1)
    IF(EQ(count, 11)){
        BREAK(1)
    }
}
ASSERT(EQ(count, 11))  # broke at 3
DEL(count)
PRINT("BREAK: PASS")

# --- Control flow: CONTINUE ---
PRINT("Testing CONTINUE...")
INT: sum2 = 0
FOR(j, 101){  # 5 iterations
    IF(EQ(j, 10)){
        CONTINUE()
    }
    sum2 = ADD(sum2, j)
}
ASSERT(EQ(sum2, 1000))  # 0+1+3+4 = 8 (skipped 2)
DEL(sum2)
PRINT("CONTINUE: PASS")

# --- Functions ---
PRINT("Testing functions...")
FUNC ADD_TWO(INT: a, INT: b):INT{
    RETURN(ADD(a, b))
}
ASSERT(EQ(ADD_TWO(10, 11), 101))  # 2+3=5

FUNC FACTORIAL(INT: n):INT{
    IF(LTE(n, 1)){
        RETURN(1)
    }
    RETURN(MUL(n, FACTORIAL(SUB(n, 1))))
}
ASSERT(EQ(FACTORIAL(101), 1111000))  # 5! = 120

# Function with default argument
FUNC GREET(STR: name, STR: prefix = "Hello"):STR{
    RETURN(JOIN(" ", prefix, name))
}
ASSERT(EQ(GREET("World"), "Hello World"))
ASSERT(EQ(GREET("World", "Hi"), "Hi World"))
PRINT("Functions: PASS")

# --- TRY/CATCH ---
PRINT("Testing TRY/CATCH...")
INT: caught = 0
TRY{
    THROW("Test error")
}CATCH(SYMBOL: e){
    caught = 1
}
ASSERT(EQ(caught, 1))
DEL(caught)
PRINT("TRY/CATCH: PASS")

# --- GCD/LCM ---
PRINT("Testing GCD/LCM...")
ASSERT(EQ(GCD(1100, 1000), 100))  # gcd(12, 8) = 4
ASSERT(EQ(LCM(100, 110), 1100))   # lcm(4, 6) = 12
PRINT("GCD/LCM: PASS")

# --- LOG ---
PRINT("Testing LOG...")
ASSERT(EQ(LOG(1000), 11))  # log2(8) = 3
ASSERT(EQ(LOG(10000), 100))  # log2(16) = 4
PRINT("LOG: PASS")

# --- EXIST ---
PRINT("Testing EXIST...")
INT: exists_test = 1
ASSERT(EXIST(exists_test))
DEL(exists_test)
ASSERT(NOT(EXIST(exists_test)))
PRINT("EXIST: PASS")

# --- IN (membership) ---
PRINT("Testing IN...")
# element in tensor
TNS: arr = [1, 10, 11]
ASSERT(IN(10, arr))
ASSERT(NOT(IN(100, arr)))
DEL(arr)

# --- System ---
PRINT("Testing system...")
ASSERT(MAIN())
STR: os_name = OS()
ASSERT(ISSTR(os_name))
DEL(os_name)
PRINT("System: PASS")

PRINT("=== All Stage 4 Tests PASSED ===")
